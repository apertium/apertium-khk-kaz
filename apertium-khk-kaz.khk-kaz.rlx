
# ========== #
# Delimiters #
# ========== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;

# ============= #
# Tags and sets #
# ============= #

SETS

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

# First level tags
# ----------------

LIST A = adj ;
LIST Adv = adv ;
LIST Pron = prn ; 
LIST N = n ;
LIST Prop = np ;
LIST V = v ;
LIST Vaux = vaux ;
LIST Cop = cop ;
LIST Det = det ; 
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;
LIST Num = num ; 
LIST Post = post ; 
LIST Postadv = postadv ; 
LIST Cm = cm ; 

# POS sub-categories
# ------------------

LIST Pers = pers ;
LIST Interr = itg ;

# "Syntactic" tags
# ----------------

LIST Advl = advl ;
LIST Attr = attr ;
LIST Subst = subst ;

# Morphosyntactic properties
# --------------------------

LIST Sg = sg ;
LIST Pl = pl ;

LIST Acc = acc ;

LIST P3 = p3 ;

# Verbal Properties 
# -----------------

LIST Prc = prc_perf prc_impf prc_cond prc_vol ;

# Semantic lists 
# --------------

LIST MOVEMENT-V = "жүгір" ; 

LIST MOVEMENT-ADV = "тиз" "акрын" "әкрен" ; 

# All possible word categories
# ----------------------------

SET WORD = N | V | A | Post | Postadv | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

SET PRE-N =  A | Det | Postadv | Num | (n gen) | (prn gen) | CC ; 

SET MARK =  Cm | ("\\") | ("\;") ;

SET WORDMARK = WORD | MARK ;

SET N-MOD = A | Det | Num | (n gen) | (prn gen) ;

SET ADJ-MOD = Postadv | Adv ;

SET NPMARK = N-MOD | ADJ-MOD ;

# Categories which cannot be part of a noun phrase 
# ------------------------------------------------

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

# Noun sets
# ---------

LIST N-SG-NOM = (n sg nom) ;
LIST N-PL-NOM = (n pl nom) ;

# Pronoun sets
# ------------

SET PRN-PERS = (prn pers) ;

# Adjective sets
# --------------

LIST A/Advl = (adj advl) ;

LIST A/Subst = (adj subst nom) ;

SET A4 = A/Advl | A/Subst ;

# Verb sets
# ---------

LIST V-P3 = (v TD aor p3 sg) (v TD neg aor p3 sg) (v TD ifi p3 sg) (v tv fut_plan p3 sg)
            (v TD aor p3 pl) (v TD neg aor p3 pl) (v TD ifi p3 pl) (v tv fut_plan p3 sg)
            (v tv ifi p3 sg) (v tv aor p3 sg) (v tv prc_vol  p3 sg) (v TD fut_plan p3 sg)
            (v tv ifi p3 pl) (v tv aor p3 pl) (v tv prc_vol  p3 pl) (v TD fut_plan p3 sg)
            (v iv aor p3 sg) (v iv ifi p3 sg) (v TD  gna_cond p3  sg) (v tv  gna_cond p3  sg) 
            (v iv aor p3 pl) (v iv ifi p3 pl) (v TD  gna_cond p3  pl) (v tv  gna_cond p3  pl)            
            (vaux pres p3 sg) (vaux neg ifi p3 sg)
            (vaux pres p3 pl) (vaux neg ifi p3 pl)
            (cop neg p3 sg)  (cop ifi p3 sg) 
            (cop neg p3 pl)  (cop ifi p3 pl) ;

               # FIXME: I suppose that this ^ is ugly, but right now I dont know
               # how else it can be done. It'll be improved as I go along with CG
               # a bit more. Any feedback appreciated ;) /I.S./

LIST Gerund = ger ger_ppot ger_past ger_perf ger_impf ger_abs ;

SET V-NotGerund = V - Gerund ;

LIST Pers-Copula = (cop p3 sg)   (cop p3 pl) ;
 
# Barriers 
# --------

SET S-BOUNDARY = CS | Interr | EOS ; 

SECTION

# some of the rules having nouns as constraints didn't work when the noun had
# personal copulas as subreadings, so I placed the rule dealing with personal
# copulas into a separate section /IS, 01-09-2012/

#
 REMOVE Pers-Copula IF
        (NOT 1 EOS)
;
#

SECTION

SELECT Adv IF (0 MOVEMENT-ADV) (0 A OR Adv) (1* MOVEMENT-V BARRIER S-BOUNDARY);

#SELECT A IF (0 A OR Adv) (1* N BARRIER NPNHA); 
# FIXME supresses a lot of correct readings

# Determiners 
# -----------

"<бір>" SELECT Det IF (-1C A) (1 N); 
    #! Әхмәт тиз генә иске зур бер агачка йөгерә.
    #! Әхмәт акрын гына иске зур бер агачка йөгерә.

"<бар>" SELECT Det IF (0 V OR Det OR A OR N) (1* NPMARK BARRIER N) ;
    #! пример : Бар<det> җирдән карый, әмма Әхмәтне таба алмый.
    #! Акча бармы? Бар<pred>.
    #! Акча бармы? Бар<pred>, ләкин бик аз!

# Nouns 
# -----

REMOVE Attr IF (NOT 1 N); 
# FIXME: this delets <loc.attr> which can appear before non-nouns

# Wordform specific    # Adding new stuff here is discouraged.  
# -----------------    # Try to generalize, if possible, existing rules.

SELECT Adv IF (0 ("өте")) (1 A);
    #! Бірақ кеше өте^өте/өт<v><TD><prc_impf>/өте<adv>/өте<v><TD><imp><p2><sg>$ суық еді!

###################   TEMPORARY HACKS TO GET THE STORY TRANSLATED   ####################
###################   SOME SHOULD BE REWRITTEN LATER                ####################

REMOVE N IF (0 ("ақырын"));
    # a temporary ugly rule for translating the story 

SELECT A/Advl IF (0 ("ақырын"));
    # a temporary ugly rule for translating the story 
    # (1* V BARRIER S-BOUNDARY) can be added after the following verb is fixed
    ## Айгүл оны ақырын<adj><advl> тыңдайды.

REMOVE N IF (0 ("жылы")) (NOT -1 N OR Num) ;
    # a temporary ugly rule for translating the story 
    #! Ауа райы бүгін әбден жақсы, ^жылы/жыл<n><px3sp><nom>/жылы<adj>/жылы<adj><subst><nom>.

###################   TO BE DONE   ####################

# get rid of plural verb reading if subject is not plural
# ^ doesn't work when sentence has an object in plural, so just remove 3P PL reading
# (btw, better idea to tag it as <p3><sp> I think)

 REMOVE Pl IF
        (0 V OR Vaux OR Cop)
        (0 P3)
;
    ## FIXME:EXTEND: subject can be a pronoun too. Or a substantivized adjective.
    ## But start with (personal) pronouns.
    #! Бірақ кеше өте суық ^еді/е<cop><ifi><p3><pl>/е<cop><ifi><p3><sg>$^!

#########################################################################################

#:
 "<аса>" SELECT Adv IF
        (1 A)
;
#  "Әл-Истахри келтірген мәліметтер аса құнды саналады."


#:
 SELECT Interj IF
         (-1 BOS)
         (1 Cm)
;
#  "Мысалы, ежелгі заманның өзінде Арал теңізі көп елдерге мәлім болған."

# get rid of postposition reading if noun/pronoun/any_subst._thing/gerund doesn't precede
 REMOVE Post IF
         (NOT -1 N OR Prop OR Pron OR Subst OR Gerund)
;
#  

# select adverbial reading (of adjectives) if any verbal form except gerund follows
 SELECT Advl IF
         (1C V-NotGerund)
;
#

#
 "<қарай>" SELECT Post IF
        (1* MOVEMENT-V)
;
#  

#
 "<жылы>" SELECT N IF
        (-1 Num)
;
#  

# select pure adjective reading ("predicative" in this case) of adjective if sentence ends
 REMOVE Advl IF
	(0 A4)
	(1 EOS)
;
 REMOVE Subst IF
         (0 A4)
         (1 EOS)
;
#

# ugly and RISKY
 "<сол>" SELECT Det IF
        (NOT 1 ("қол") OR ("жақ"))
;
#  

#
 "<ал>" REMOVE CC IF
	(NOT -1 BOS OR Cm )
;
# Бөлмеде шкаф бар, содан бiр кiтап ал.


#
 REMOVE Prc IF
        (NOT 1C V OR Vaux)
;
#


#
 SELECT A IF
        (0 ("құрама"))
        (-1 (np top))
;
# 69. Франция құрамасының бұрынғы ойыншысы Мишель Платини...

# FIXME: I wanted to refer "жәдігер" as a lemma, but it didn't work
# (because it has cop p3 subreding I guess)
 "<жинағы>" SELECT N IF
        (-1 ("<жәдігерлер>"))
;
# 165. ...тарихи жәдігерлер жинағы арқылы көрсету.

#
 SELECT N IF
        (0 ("қор"))
        (-1 N)
; 
# 169. Музей қоры Қазақстан Республикасы Мәдениет және ақпарат министрінің...


# UGLY
# "сый" is certainly not <v.iv.imp> in the following example,
# I don't think that сый<imp> is ever used
 "<сый>" SELECT N
; 
# 171. Жеке және заңды тұлғалардан _сый_ немесе мұра ретінде қабылданған заттар есебінен;


# if a word is ambigious between verbal adjective and gerund (in nominative - if there is a case affix it's a bit
# complicated since the verbal adjective can be substantivized), select verbal adverb reading if a noun follows
# SELECT V-ADJECTIVE IF
#        (0 Gerund (nom))
#        (0 V-ADJECTIVE)
#        (1 N)
#;


# RISKY
#REMOVE Acc IF (0 ("қызықты")) (NOT 1 V);
 REMOVE Acc IF
        (0 (n acc) OR (adj subst acc))
        (0 A)
        (NOT 1 V)
;
#! Еуро-2012 ^туралы/туралы<post>$ ^қызықты/қызық<adj><subst><acc>/қызықты<adj>/қызықты<adj><advl>/қызықты<adj><subst><nom>$ ^статистикаға/статистика<n><dat>$ көз салыңыз.


#
 "<қарасты>" SELECT A IF
        (-1 (dat))
;
#  177. Орталыққа _қарасты_ Түркістан қаласында Н.Оңдасынов атындағы тарихи-мемориалдық мұражайы атты филиал жұмыс жасайды.


#
 "<атты>" REMOVE V IF
        (NOT 1 EOS OR Cm)
; 
#


# FIXME CHECK
# the three ambiguities above are quite common - an adjective (A4) with -LI is
# ambiguous with: 1) a noun or adjective in accusative, and 2) with  a verb in
# <ifi> form. So it is a good idea to have a more general rule:
 REMOVE V IF
        (0 A)
        (0 (ifi))
        (NOT 1 EOS OR Cm)
;
#

# ambigious with v.pass
 "<құрал>" SELECT N IF
        (-1 A OR (n attr))
; 
#


# imperative of "ісу" ("to swell") - just like "<сый>" above sounds odd,
# but let's try to deal with it in a less ugly way /IS/
 "<іс>" SELECT N IF
        (-1 A)
; 
# 178. ...тәрбиелік мәні бар іс-шараларды бірге ұйымдастыру...


# FIXME: could be achieved with a more general rule I think
 "<көрме>" SELECT N IF
        (1 ("зал"i))
; 
# 181. Ғимарат 3 қабаттан, 5 көрме залынан, конференция залынан, қолөнершілердің шеберханасы...


#
 "<көптеген>" SELECT Det IF
        (1 N + Pl)
; 
#

#
 SELECT Prop IF
        (0 N)
        (0 Prop)
        (0 ("[:upper:][:lower:]+"r))
        (NOT -1 BOS)  ;
# 108. Өйткені бүгінгі _Арал_ деген атау сол XVII ғасырдан бергі жерге берілген.


# FIXME: generealize
 "<қайта>" SELECT Adv IF
        (1 ("баста"i))
; 
#
