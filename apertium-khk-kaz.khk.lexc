!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!  M O R P H O L O G I C A L · T R A N S D U C E R · F O R · K H A L K H A  !!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! See http://wiki.apertium.org/wiki/Turkic_lexicon

Multichar_Symbols
! Followed http://wiki.apertium.org/wiki/Turkic_languages

! Part of speech categories
%<n%>         ! Noun                       ! Зат есім
%<np%>        ! Proper noun
%<adj%>       ! Adjective                  ! Сын есім
%<num%>       ! Numeral                    ! Сан есім
%<prn%>       ! Pronoun
%<det%>       ! Determiner
%<v%>         ! Verb
%<vaux%>      ! Auxilary verb
%<adv%>       ! Adverb                     ! Үстеу            ! Наречие
%<post%>      ! Postposition               ! Септеулік шылау
%<postadv%>   ! Postadverb                 ! "Артүстеу"
%<cnjcoo%>    ! Co-ordinating conjunction  ! жалғаулық шылау
%<cnjsub%>    ! Sub-ordinating conjunction ! жалғаулық шылау
%<cnjadv%>    ! Adverbial conjunction      ! жалғаулық шылау
%<part%>      ! Particle
%<ij%>        ! Interjection               ! Одағай
%<abbr%>      ! Abbreviation
%<cop%>       ! Copula

! Proper noun types
%<top%>       ! Toponym
%<ant%>       ! Anthroponym
%<cog%>       ! Cognomen
%<pat%>       ! Patronym     ! Отчество
%<org%>       ! Organization
%<al%>        ! Other

!! gender of anthroponyms and cognoms
%<m%>         ! Masculine
%<f%>         ! Feminine
%<mf%>        ! Masculine/feminine !# basically cognoms without -ов/-ова,
              ! -ин/-ина endings

! "Syntactic" tags. Attributive use of non-adjectives etc.
%<attr%>      ! Attributive
%<subst%>     ! Substantive
%<advl%>      ! Adverbial

! Number
%<sg%>        ! Singular
%<pl%>        ! Plural

! Possessives
%<px1sg%>     ! First person singular
%<px2sg%>     ! Second person singular
%<px3sp%>     ! Third person singular/plural
%<px1pl%>     ! First person plural
%<px2pl%>     ! Second person plural
%<px%>        ! General possessive          !# -нікі

! Cases
%<nom%>       ! Nominative
%<gen%>       ! Genitive
%<gens%>      ! Short/clitic Genitive (used with nouns)
%<dat%>       ! Dative
%<acc%>       ! Accusative
%<abl%>       ! Ablative
%<loc%>       ! Locative
%<ins%>       ! Instrumental

%<refl%>      ! Reflexive

!! some additional ~cases
%<sim%>       ! Similative (<apprx> in kymorph)
              !# DAй
%<abe%>       ! Abessive=Privative ! Лишительный
              !# -сыз/-сез (not used after posessives and cases)
%<reas%>      ! Reason

! Levels of comparison of adjectives
%<comp%>      ! Comparative
%<sup%>       ! Superlative

! Pronoun types
%<pers%>      ! Personal
%<recip%>     ! Reciprocal
! Pronoun and determiner types
%<dem%>       ! Demonstrative
%<ind%>       ! Indefinite
%<itg%>       ! Interrogative
%<qnt%>       ! Quantifier
%<neg%>       ! Negative       !# еш нерсе
              ! (NOTE: also used to denote negation in verbs, i.e for м{A})
%<ref%>       ! Reflexive

! Numeral types
%<ord%>       ! Ordinal
%<coll%>      ! Collective
%<dist%>      ! Distributive

! Verbal features

!! Mood
%<imp%>       ! Imperative
%<opt%>       ! Optative/jussive
%<sug%>       ! Suggestion

!! Derivation
%<caus%>      ! Causative
%<pass%>      ! Passive
%<coop%>      ! Cooperative

!! Tense / finite forms
%<pres%>      ! for "жатыр", "тұр", "отыр" and "жүр"
%<aor%>       ! -{E}                    ! non-past (afaik /IS/)
%<past%>      ! -{G}{A}н                ! past
%<ifi%>       ! -{D}{I}                 ! eyewitness past
%<hab%>       ! habitual
%<ifi_evid%>  ! -{I}пт{I}               ! "reported past" (afaik /IS/)
%<fut%>       ! -{I}р                   ! future
%<fut_plan%>  ! -{M}{A}{K}              ! future plan

%<evid%>      ! evidential
              ! Was used for -ган екен and -ыпты. The former is not handled here
              ! in lexc anymore, the latter was renamed from <ifi><evid> to
              ! <ifi_evid>

!! Non-Finite verb forms
%<gna%>       ! generic verbal adverb
%<gna_prf%>   ! perfective verbal adverb
%<gna_imp%>   ! imperfective verbal adverb
%<gna_cnd%>   ! conditional verbal adverb
%<gna2%>      ! "until" verbal adverb
%<ger%>       ! future gerund
%<ger_imp%>   ! imperfective gerund
%<ger_prf%>   ! perfective gerund

!!! Participles

!! Transitivity
%<tv%>        ! Transitive
%<iv%>        ! Intransitive
%<TD%>        ! Temporary tag

! Person
%<p1%>        ! First person
%<p2%>        ! Second person
%<p3%>        ! Third person
%<frm%>       ! Formality

! Modal particles
%<qst%>       ! Modal question particle
%<mod_foc%>   ! "Particles of politeness and ardent desire"
              ! (as in [N. Poppe. Tatar Manual]) or
              ! "what about ..." question particles" :)
              ! (as in kir.lexc)
%<mod_ass%>   ! Assertive modal particle
%<mod%>       ! Modal verbs (шығар, сияқты etc)

! Punctuation
%<sent%>      ! Sentence marker
%<guio%>      ! Hyphen
%<cm%>        ! Comma
%<apos%>      ! Apostrophe
%<rquot%>     ! Quote marker (right hand side)
%<lquot%>     ! Quote marker (left hand side)
%<rpar%>      ! Parenthetical marker (right hand side)
%<lpar%>      ! Parenthetical marker (left hand side)

%{г%}
%{й%}
%{а%}
%{А%}
%{А1%}
%{А2%}
%{I%}
%{ь%}
%{В%}
%{Т%}
%{У%}
%{Д%}
%{н%}
%{Н%}
%{ң%}         ! for nouns ending in н that's /ŋ/ and not /n/→[ŋ]
%{ъ%}         ! blocks voicing assimilation, mostly of Д for "irregular" locatives and Ж irregular verbal adverbs
%{Ж%}

%>            ! Morpheme boundary
%             ! Space
%-            !
%,            !
%.            !
%%            !

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                       M O R P H O T A C T I C S                         !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

Nouns ;
Verbs ;
Pronouns ;
Conjunctions ;
Adverbs ;
Adjectives ;
Postpositions ;
Postadverbs ;
Determiners ;
Numbers ;
Particles ;     ! UGH
ProperNouns ;


LEXICON Clit
! Sentence-final clitics

# ;
+вэ%<qst%>:% %{В%}э # ;         ! wh-question
+уу%<qst%>:% %{У%}%{У%} # ;     ! yes/no-question
+даа%<qst%>:% д%{А1%}%{А2%} # ;   !
+аа%<qst%>:% %{А1%}%{А2%} # ;     !
+билээ%<qst%>:% билээ # ;       ! assumed previously known information (?)


LEXICON Cases-Normal

%<nom%>: Clit ;
%<acc%>:%>%{I%}%{й%}г Clit ;

%<refl%>:%>%{г%}%{А1%}%{А2%} Clit ;  ! this is both nom and acc

%<gen%>:%>%{г%}%{I%}%{й%}н Clit ;
%<gen%>%<refl%>:%>%{г%}%{I%}%{й%}нх%>%{А1%}%{А2%} Clit ;

%<abl%>:%>%{г%}%{А1%}%{А2%}с Clit ;
%<abl%>%<refl%>:%>%{г%}%{А1%}%{А2%}с%{А1%}%{А2%} Clit ;

%<inst%>:%>%{г%}%{А1%}%{А2%}р Clit ;
%<inst%>%<refl%>:%>%{г%}%{А1%}%{А2%}р%>%{А1%}%{А2%} Clit ;

%<dat%>:%>%{а%}%{Д%} Clit ;               ! (with exceptional forms in т)
%<dat%>%<refl%>:%>д%{А1%}%{А2%} Clit ;  ! (with exceptional forms in т)

%<dat%>%<attr%>:% д%{А%}х%{ь%} Clit ;

%<gens%>:%>%{а%}н # ;


LEXICON Cases-ExtraN

%<nom%>: Clit ;
%<acc%>:%>%{I%}%{й%}г Clit ;

%<refl%>:%>%{г%}%{А1%}%{А2%} Clit ;  ! this is both nom and acc

%<gen%>:%>%{Н%}%{I%}%{й%} Clit ;
%<gen%>%<refl%>:%>%{Н%}%{I%}%{й%}нх%>%{А1%}%{А2%} Clit ;

%<abl%>:%>%{Н%}%{А1%}%{А2%}с Clit ;
%<abl%>%<refl%>:%>%{Н%}%{А1%}%{А2%}с%>%{А1%}%{А2%} Clit ;

%<inst%>:%>%{Н%}%{А1%}%{А2%}р Clit ;
%<inst%>%<refl%>:%>%{Н%}%{А1%}%{А2%}р%>%{А1%}%{А2%} Clit ;

%<dat%>:%>%{а%}%{Н%}д Clit ;               ! (with exceptional forms in т)
%<dat%>%<refl%>:%>%{Н%}д%{А1%}%{А2%} Clit ;  ! (with exceptional forms in т)

%<dat%>%<attr%>:% д%{А%}х%{ь%} Clit ;

%<gens%>:%>%{а%}н # ;

LEXICON PrnInfl

%<prn%>: Cases-Normal ;

LEXICON DetInfl

%<det%>: # ;

LEXICON Ninfl

%<n%>: Cases-Normal ;
%<n%>%<pl%>:%>%{н%}%{У%}%{У%}д Cases-Normal ;  ! (with exceptional forms)

LEXICON Ninfl-Person

%<n%>: Cases-Normal ;
%<n%>%<pl%>:%>% нар Cases-Normal ;

LEXICON Ninfl-NoPl

%<n%>: Cases-Normal ;

LEXICON Ninfl-Pl

%<n%>%<pl%>: Cases-Normal ;


LEXICON Ninfl-ExtraN

%<n%>: Cases-ExtraN ;
%<n%>%<pl%>:%>н%{У%}%{У%}д Cases-Normal ;  ! (with exceptional forms)

LEXICON Ninfl-ExtraN-NoPl

%<n%>: Cases-ExtraN ;

LEXICON NP-COG-M

%<np%>%<cog%>%<m%>: Cases-Normal ;

LEXICON NP-TOP

%<np%>%<top%>: Cases-Normal ; 

LEXICON Vinfl-Common

%<aor%>:%>н%{А%} # ;
%<aor%>%<neg%>:%>%{а%}хгүй Clit ;

%<past%>:%>с%{а%}н Clit ;
%<past%>%<neg%>:%>%{г%}%{А1%}%{А2%}гүй Clit ;
%<past%>:%>%{а%}в # ;  ! FIXME: figure this out

%<past%>%<evid%>:жээ # ;

%<ifi%>:%>л%{А1%}%{А2%} # ;
%<hab%>:%>д%{а%}г Clit ;

%<ger%>:%>%{а%}х Cases-Normal ;
%<ger_imp%>:%>д%{а%}г Cases-Normal ;
%<ger_prf%>:%>с%{а%}н Cases-Normal ;

%<gna%>:%>%{а%}%{Ж%} # ;
%<gna_imp%>:%>%{а%}н # ;
%<gna_prf%>:%>%{г%}%{А1%}%{А2%}д # ;
%<gna_cnd%>:%>%{В%}%{А%}л # ;
%<gna_cnd%>:%>%{Т%}%{А%}л # ;

%<imp%>:%>%{А1%}%{А2%}р%{А%}й # ;     ! simple imperative
%<sug%>:%>%{А1%}%{А2%}ч # ;           ! suggestion

LEXICON Vinfl-TV

%<v%>%<tv%>: Vinfl-Common ;

LEXICON Vinfl-IV

%<v%>%<iv%>: Vinfl-Common ;

LEXICON Pronouns

би%<prn%>%<p1%>%<sg%>%<nom%>:би Clit ;
би%<prn%>%<p1%>%<sg%>%<acc%>:намайг Clit ;
би%<prn%>%<p1%>%<sg%>%<gen%>:миний Clit ;
би%<prn%>%<p1%>%<sg%>%<abl%>:надаас Clit ;
би%<prn%>%<p1%>%<sg%>%<dat%>:надад Clit ;
би%<prn%>%<p1%>%<sg%>%<inst%>:надаар Clit ;
би%<prn%>%<p1%>%<sg%>%<px%>:минийх Clit ;    ! FIXME: check
би%<prn%>%<p1%>%<sg%>%<gens%>:минь Clit ;

бид%<prn%>%<p1%>%<pl%>%<nom%>:бид Clit ;
бид%<prn%>%<p1%>%<pl%>%<acc%>:биднийг Clit ;
бид%<prn%>%<p1%>%<pl%>%<gen%>:бидний Clit ;
бид%<prn%>%<p1%>%<pl%>%<abl%>:биднээс Clit ;
бид%<prn%>%<p1%>%<pl%>%<dat%>:бидэнд Clit ;
бид%<prn%>%<p1%>%<pl%>%<inst%>:биднээр Clit ;
бид%<prn%>%<p1%>%<pl%>%<px%>:биднийх Clit ; ! FIXME: Check

чи%<prn%>%<p2%>%<sg%>%<nom%>:чи Clit ;
чи%<prn%>%<p2%>%<sg%>%<acc%>:чамайг Clit ;
чи%<prn%>%<p2%>%<sg%>%<gen%>:чиний Clit ;
чи%<prn%>%<p2%>%<sg%>%<abl%>:чамаас Clit ;
чи%<prn%>%<p2%>%<sg%>%<dat%>:чамд Clit ;
чи%<prn%>%<p2%>%<sg%>%<inst%>:чамаар Clit ;  ! FIXME: check
чи%<prn%>%<p2%>%<sg%>%<px%>:чинийх Clit ;    ! FIXME: check
чи%<prn%>%<p2%>%<sg%>%<gens%>:чинь Clit ;

та%<prn%>%<p2%>%<frm%>%<nom%>:та Clit ;
та%<prn%>%<p2%>%<frm%>%<acc%>:таныг Clit ;
та%<prn%>%<p2%>%<frm%>%<gen%>:таны Clit ;
%та%<prn%>%<p2%>%<frm%>%<abl%>:та Clit ;    ! FIXME: check
та%<prn%>%<p2%>%<frm%>%<dat%>:танд Clit ;
%та%<prn%>%<p2%>%<frm%>%<inst%>:та Clit ;  ! FIXME: check
та%<prn%>%<p2%>%<frm%>%<px%>:таных Clit ;   ! FIXME: check
та%<prn%>%<p2%>%<frm%>%<gens%>:тань Clit ;

тэр%<prn%>%<p3%>%<sg%>%<nom%>:тэр Clit ;
тэр%<prn%>%<p3%>%<sg%>%<acc%>:түүнийг Clit ;
тэр%<prn%>%<p3%>%<sg%>%<gen%>:түүний Clit ;
тэр%<prn%>%<p3%>%<sg%>%<abl%>:түүнээс Clit ;
тэр%<dem%>%<p3%>%<sg%>%<dat%>:тэнд Clit ;     ! loc
тэр%<prn%>%<p3%>%<sg%>%<dat%>:түүнд Clit ;    ! dat/loc
тэр%<prn%>%<p3%>%<sg%>%<inst%>:тэрээр Clit ;  ! FIXME: check
тэр%<prn%>%<p3%>%<sg%>%<px%>:түүнийх Clit ;   ! FIXME: check
тэр%<prn%>%<p3%>%<sg%>%<gens%>:нь Clit ;

тэд%<prn%>%<p3%>%<pl%>%<nom%>:тэд Clit ;
тэд%<prn%>%<p3%>%<pl%>%<acc%>:тэднийг Clit ;
тэд%<prn%>%<p3%>%<pl%>%<gen%>:тэдний Clit ;
тэд%<prn%>%<p3%>%<pl%>%<abl%>:тэднээс Clit ;
тэд%<prn%>%<p3%>%<pl%>%<dat%>:тэдэнд Clit ;   ! FIXME: check
тэд%<prn%>%<p3%>%<pl%>%<inst%>:тэднээр Clit ; ! FIXME: check
тэд%<prn%>%<p3%>%<pl%>%<px%>:тэднийх Clit ;   ! FIXME: check
тэд%<prn%>%<p3%>%<pl%>%<gens%>:нь Clit ;

хэн PrnInfl ;            ! "who"
юу PrnInfl ;             ! "what"
өөр PrnInfl ;            ! "oneself"
энэ:эн PrnInfl ;         ! "this one"
ямар:ям%{а%}р PrnInfl ;  ! "which one"
тийм:тийм PrnInfl ;      ! "one like that"
ийм:ийм PrnInfl ;        ! "one like this"
бүгд:бүгд PrnInfl ;      ! "everyone"

LEXICON Determiners

энэ:энэ DetInfl ;        ! "this"
ямар:ям%{а%}р DetInfl ;  ! "which"
бүх:бүх DetInfl ;        ! "entire, all, the whole"
тийм:тийм DetInfl ;      ! "such a"
ийм:ийм DetInfl ;        ! "such a"
бүгд:бүгд DetInfl ;      ! "all"

LEXICON Num

%<num%>: Cases-Normal ;

LEXICON Numbers

нэг:нэг Num ;            ! "one"
хоёр:хоёр Num ;          ! "two"
гурав:гур%{а%}в Num ;    ! "three"
дөрөв:дөр%{а%}в Num ;    ! "four"
тав:тав Num ;            ! "five"
зургаа:зургаа Num ;      ! "six"
долоо:долоо Num ;        ! "seven"
найм:найм Num ;          ! "eight"
ёс:ёс Num ;              ! "nine"
арав:ар%{а%}в Num ;      ! "ten"
зуун:зуун Num ;          ! "hundred"
мянга:мянга Num ;        ! "thousand"

LEXICON ConjCoo

%<cnjcoo%>: # ;


LEXICON Postp

%<post%>: # ;


LEXICON PostAdv-Infl

%<postadv%>: # ;


LEXICON Adj

%<adj%>: # ;


LEXICON Adv

%<adv%>: # ;


LEXICON Nouns

цаг:цаг Ninfl ; ! "time/hour"
цаг:цаг Ninfl-ExtraN ; ! "watch/clock"
гар:гар Ninfl ; ! "hand"
нас:нас Ninfl ; ! "age"
ном:ном Ninfl ; ! "book"
хүүхэд:хүүх%{а%}д Ninfl ; ! "child"
хүн:хүн Ninfl-NoPl ; ! "person"
хүн:хүмүүс Ninfl-Pl ; ! "person"
цус:цус Ninfl ; ! "blood"

LEXICON Verbs

хар:хар%{ъ%} Vinfl-TV ; ! "look at"
ир:ир%{ъ%} Vinfl-IV ; ! "come"
яв:яв%{ъ%} Vinfl-IV ; ! "go"
оч:оч Vinfl-IV ; ! "go"
сонго:сонго Vinfl-TV ; ! "choose"
ойлго:ойлго Vinfl-TV ; ! "understand"

LEXICON Conjunctions

бөгөөд:бөгөөд ConjCoo ; ! "along with" ?
ба:ба ConjCoo ; ! "and"
бас:бас ConjCoo ; ! "and"
харин:харин ConjCoo ; ! ~"but"
эсвэл:эсвэл ConjCoo ; ! "if"
гэвч:гэвч ConjCoo ; ! "but"

LEXICON Postpositions

хүртэл:хүртэл Postp ;  ! "until"

LEXICON Postadverbs

л:л PostAdv-Infl ; ! "only"

LEXICON Adverbs

сайн:сайн Adv ; ! "well"
их:их Adv ; ! "very"
дахин:дахин Adv ; ! "again"
одоо:ороо Adv ; ! "now"
сая:сая Adv ; ! "just now"
ягаад:ягаад Adv ; ! "why"
тийм:тийм Adv ; ! "in such a way"
илүү:илүү Adv ; ! "rather"

LEXICON Adjectives

ариун:ариун Adj ; ! "pure"
сайхан:сайхан Adj ; ! "good"
муу:муу Adj ; ! "bad"
зөв:зөв Adj ; ! "correct"
хэрэгтэй:хэрэгтэй Adj ; ! "necessary"
шинэ:шинэ Adj ; ! "new"
том:том Adj ; ! "big"
жижиг:жиж%{а%}г Adj ; ! "small"
жижигхэн:жижигх%{а%}н Adj ; ! "little"

LEXICON Particles

бүү%<imp%>%<neg%>:бүү # ;   ! negative imperative particle ≈ "don't ___ !"
шүү%<mod_ass%>:шүү # ;      ! assertive modal particle

LEXICON ProperNouns

Иерусалим:Иерусалим NP-TOP ; ! "Jerusalem"
